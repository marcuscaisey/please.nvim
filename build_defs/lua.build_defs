def lua_toolchain(name:str, version:str, hash:str):
    """
    Downloads and compiles Lua and exposes the lua binary as the entry point :<name>|lua.

    To use this rule, add the following to your .plzconfig:

    [BuildConfig]
    lua-tool = //...:<name>|lua

    Args:
        name: Name of the target.
        version: The version of Lua to download.
        hash: A hash for the downloaded archive.
        visibility: Visibility specification. Defaults to public.
    """
    platforms_by_os = {
        "linux": "linux",
        "darwin": "macosx",
    }
    if CONFIG.HOSTOS not in platforms_by_os:
        fail(f'lua_toolchain rule is not supported on OS: "{CONFIG.HOSTOS}"')
    platform = platforms_by_os[CONFIG.HOSTOS]

    download = remote_file(
        name = tag(name, "download"),
        hashes = [hash],
        url = f"https://www.lua.org/ftp/lua-{version}.tar.gz",
    )

    return genrule(
        name = name,
        srcs = {
            "download": download,
            "patch": "//build_defs:remove_readline_from_lua",
        },
        outs = [name],
        binary = True,
        building_description = "Installing...",
        cmd = [
            "tar -xzvf $SRCS_DOWNLOAD",
            f"cd lua-{version}",
            "patch -p1 <$TMP_DIR/$SRCS_PATCH",
            f"make {platform}",
            "make INSTALL_TOP=$OUT install",
        ],
        entry_points = {
            "lua": f"{name}/bin/lua",
        },
    )

def luarocks(name:str, version:str):
    """
    Downloads LuaRocks.

    To use this rule, add the following to your .plzconfig:

    [BuildConfig]
    luarocks-tool = //...:<name>|luarocks

    You must have lua-tool configured under the [BuildConfig] section in your .plzconfig. lua-tool is the lua binary
    used to run luarocks. This can either be a path or the |lua entry point which is generated by the lua_toolchain
    build rule.

    For example:

    [BuildConfig]
    lua-tool = //third_party/lua:toolchain|lua

    Args:
        name: Name of the target.
        version: The version of LuaRocks to download.
        visibility: Visibility specification. Defaults to public.
    """
    lua = check_config(
        example = "//third_party/lua:toolchain|lua",
        key = "LUA_TOOL",
        rule = "luarocks",
        section = "BuildConfig",
    )

    download = remote_file(
        name = tag(name, "download"),
        url = f"https://github.com/luarocks/luarocks/archive/refs/tags/v{version}.tar.gz",
    )

    return genrule(
        name = name,
        srcs = [download],
        outs = [name],
        binary = True,
        building_description = "Installing...",
        cmd = [
            "tar -xzvf $SRCS",
            f"cd luarocks-{version}",
            f"./configure --prefix=$OUT --with-lua-bin=$(dirname $TOOLS_LUA)",
            "make",
            "make install",
            """sed -i'' -e '1a\\\npackage.path = arg[0]:gsub("bin/luarocks$", string.format("share/lua/%s/?.lua;", _VERSION:match("%d+%.%d+"))) .. package.path\n' $OUT/bin/luarocks""",
        ],
        entry_points = {
            "luarocks": f"{name}/bin/luarocks",
        },
        tools = {
            "lua": lua,
        },
    )

def luarocks_package(name:str, package:str=None, version:str, deps:list=[], visibility:list=["PUBLIC"]):
    """
    Downloads a LuaRocks package.

    You must have luarocks-tool configured under the [BuildConfig] section in your .plzconfig. luarocks-tool is the
    binary used to run luarocks. This can either be a path or the |luarocks entry point which is generated by the
    luarocks build rule.

    For example:

    [BuildConfig]
    luarocks-tool = //third_party/lua:luarocks|luarocks

    Args:
        name: Name of the build target.
        package: Name of the package. Defaults to name.
        version: Version to download.
        deps: Dependencies of the package. These should be luarocks_package targets. Optional.
        visibility: Visibility specification. Defaults to public.
    """
    luarocks = check_config(
        example = "//third_party/lua:luarocks|luarocks",
        key = "LUAROCKS_TOOL",
        rule = "luarocks_package",
        section = "BuildConfig",
    )
    package = package or name
    return genrule(
        name = name,
        cmd = [
            f"$TOOL --tree=tree install --deps-mode=none {package} {version}",
            "mkdir _out",
            "if [ -d tree/share/lua ]; then cp -R $(find tree/share/lua -mindepth 2 -maxdepth 2) _out; fi",
            "if [ -d tree/lib/lua ]; then cp -R $(find tree/lib/lua -mindepth 2 -maxdepth 2) _out; fi",
        ],
        exported_deps = deps,
        output_dirs = ["_out"],
        sandbox = False,
        tools = [luarocks],
        visibility = visibility,
    )
